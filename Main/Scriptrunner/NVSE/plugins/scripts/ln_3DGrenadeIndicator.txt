; 3D Grenade Indicator 1.6 - Stentorious
if Goo1.AuxVarGetFlt "*3DGrenade_Init" != 1																										; On game restart
	Goo1.AuxVarSetFlt "*3DGrenade_Init" 1
	; Check for requirements
	if GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 2
	else
		MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall xNVSE 6.3.3+."
		return
	endif 
	if GetPluginVersion "JohnnyGuitarNVSE" < 390
		MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
		return
	endif
	SetGameMainLoopCallback (CompileScript "3DGrenadeIndicator/MainLoop.gek") 1 10 1															; Set Main Loop
	Goo1.AuxVarSetFlt "*3DGrenadeDist" ((GetMaxOf 5 (GetMinOf 100 (GetINIFloat "General:fMaxDistance" "3DGrenadeIndicator.ini"))) * 69.99104)	; Get indicator visibility radius INI setting
	SetUIFloatAlt "HUDMainMenu\3DGrenade\_RadiusMax" (Goo1.AuxVarGetFlt "*3DGrenadeDist")
	SetUIFloatAlt "HUDMainMenu\3DGrenade\_ShowTimer" (GetINIFloat "General:bShowFuseTimer" "3DGrenadeIndicator.ini")								; Get fuse timer INI setting
	if eval GetINIFloat "General:bPulsingIcon" "3DGrenadeIndicator.ini" == 1																	; Get Pulsing Opacity INI setting
		SetUIFloatGradual "HUDMainMenu\3DGrenade\_AlphaPulse" 0 0.65 0.3 2																		; Get set damage radius pulsing alpha
	endif		
	if eval (FileExists "textures\interface\xlrQT\FalloutNV.esm4330.dds" && GetINIFloat "General:bB42Icons" "3DGrenadeIndicator.ini" == 1)		; Get B42 Quickthrow support INI setting
		Goo1.AuxVarSetFlt "*3DGrenadeB42" 1
	endif
	if eval GetINIFloat "General:bB42IconsColorFix" "3DGrenadeIndicator.ini" == 1																; Get B42 Quickthrow color fix INI setting
		Goo1.AuxVarSetFlt "*3DGrenadeColor" 1
	endif
endif

; Check for requirements
if GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 2
else
	return
endif 
if GetPluginVersion "JohnnyGuitarNVSE" < 390
	return
endif
SetJohnnyOnRenderUpdateEventHandler 0 (CompileScript "3DGrenadeIndicator/OnRender.gek")															; Remove On Render event handler
UnloadUIComponent "HUDMainMenu\3DGrenade\3DGRect"																								; Remove parent tile
Goo1.AuxVarErase "*3DGrenadeRef"																												; Clear tracked refs
Goo1.AuxVarErase "*3DGrenadeRefCache"																											; Clear cached refs
Goo1.AuxVarErase "*3DGrenadeTile"																												; Clear tile count
