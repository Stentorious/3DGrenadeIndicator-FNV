; 3D Grenade Indicator - Stentorious

; On game restart
if Goo1.AuxVarGetFlt "*3DGrenade_Init" 0 != 1
	Goo1.AuxVarSetFlt "*3DGrenade_Init" 1 0

	; Check for requirements
	if GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 5
	else
		MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall xNVSE 6.3.6+."
		return
	endif
	if GetPluginVersion "JohnnyGuitarNVSE" < 390
		MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
		return
	endif
	if GetPluginVersion "UI Organizer Plugin" < 230
		MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall latest User Interface Organizer plugin."
		return
	endif
	Goo1.AuxVarSetFlt "*3DGrenade_Init" 1 1

	; Load INI settings
	Goo1.AuxVarSetFlt "*3DGrenade_INI" (eval FileExists "textures\interface\xlrQT\FalloutNV.esm4330.dds" && GetINIFloat "B42 Quickthrow:bUseIcons" "Stentorious\3DGrenadeIndicator.ini" == 1) 0
	Goo1.AuxVarSetFlt "*3DGrenade_INI" (GetINIFloat "B42 Quickthrow:bInterfaceTint" "Stentorious\3DGrenadeIndicator.ini") 1
	Goo1.AuxVarSetFlt "*3DGrenade_INI" ((GetMaxOf 5 (GetMinOf 100 (GetINIFloat "General:fMaxDistance" "Stentorious\3DGrenadeIndicator.ini"))) * 69.99104) 2
	SetUIFloatAlt "HUDMainMenu\3DGrenade\_DistMax" (Goo1.AuxVarGetFlt "*3DGrenade_INI" 2)
	SetUIFloatAlt "HUDMainMenu\3DGrenade\_Timer" (GetINIFloat "General:bFuseTimer" "Stentorious\3DGrenadeIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\3DGrenade\_DropShadow" (GetINIFloat "General:bDropShadow" "Stentorious\3DGrenadeIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\3DGrenade\_IconScale" (GetINIFloat "General:fIconSize" "Stentorious\3DGrenadeIndicator.ini")
	if eval GetINIFloat "General:bPerceptionCalc" "Stentorious\3DGrenadeIndicator.ini"
		SetUIFloatAlt "HUDMainMenu\3DGrenade\_DistMode" 1
		SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fOldValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\3DGrenade\_PlrAV6" (GetMaxOf 1 (GetMinOf 10 (Player.GetAV Perception)))) 0 6
	endif
	if eval GetINIFloat "General:bPulsingIcon" "Stentorious\3DGrenadeIndicator.ini"
		SetUIFloatGradual "HUDMainMenu\3DGrenade\_AlphaPulse" 0 0.65 0.3 2
	endif
	if eval GetINIFloat "General:iIconStyle" "Stentorious\3DGrenadeIndicator.ini" == 0
		SetUIStringAlt "HUDMainMenu\3DGrenade\_Icon" "interface\icons\typeicons\hot_keys_icon_throwweapons.dds"
	endif

	SetGameMainLoopCallback (CompileScript "3DGrenadeIndicator/MainLoop.gek") 1 10 1

endif

; On load
if eval Goo1.AuxVarGetFlt "*3DGrenade_Init" 1
	; Reset all icons
	SetJohnnyOnRenderUpdateEventHandler 0 (CompileScript "3DGrenadeIndicator/OnRender.gek")
	UnloadUIComponent "HUDMainMenu\3DGrenade\Icons"
	Goo1.AuxVarErase "*3DGrenade_Ref"
	Goo1.AuxVarErase "*3DGrenade_Cache"
	; Update perception calc
	if eval GetUIFloatAlt "HUDMainMenu\3DGrenade\_DistMode"
		SetUIFloatAlt "HUDMainMenu\3DGrenade\_PlrAV6" (GetMaxOf 1 (GetMinOf 10 (Player.GetAV Perception)))
	endif
endif