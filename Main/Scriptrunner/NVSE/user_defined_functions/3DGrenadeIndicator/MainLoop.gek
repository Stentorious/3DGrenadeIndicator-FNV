int iIndex
int iTiles
int iFlag
int iB42Icon
int iB42Color
ref rProjectile
ref rWeapon
ref rExplosion
array_var aGrenades
array_var aProjectiles
array_var aCache
array_var aForE
string_var sUIPath
string_var sTexture

begin Function {}

	; Get INI settings
	iB42Icon = Goo1.AuxVarGetFlt "*3DGrenade_INI" 0
	iB42Color = Goo1.AuxVarGetFlt "*3DGrenade_INI" 1

	; Validate icons refs and hide invalid icons
	iTiles = Goo1.AuxVarGetFlt "*3DGrenade_Flt"
	if eval iTiles > 0
		foreach aForE <- Goo1.AuxVarGetAsArr "*3DGrenade_Ref"
			rProjectile = *aForE

			; Grenade ref
			if eval IsFormValid rProjectile && IsReference rProjectile && GetWeaponType (rProjectile.GetProjectileRefWeapon) == 10
				iFlag = 1

			; Invalid ref
			elseif eval *aForE != -1
				iIndex = aForE["key"]
				Goo1.AuxVarSetFlt "*3DGrenade_Ref" -1 iIndex
				SetUIFloatAlt ("HUDMainMenu\3DGrenade\3DGRect\3DGInj" + $iIndex + "\visible") 0
			endif

		loop

		; Reset icons rect if no valid refs
		if iFlag != 1
			iTiles = 0
			Goo1.AuxVarErase "*3DGrenade_Ref"
			UnloadUIComponent "HUDMainMenu\3DGrenade\3DGRect"
			SetJohnnyOnRenderUpdateEventHandler 0 (CompileScript "3DGrenadeIndicator/OnRender.gek")
		endif

	endif

	; Cache nearby projectile refs
	aProjectiles = Player.GetRefs 51 2 1 (Goo1.AuxVarGetFlt "*3DGrenade_INI" 2)
	aCache = Goo1.AuxVarGetAsArr "*3DGrenade_Cache"
	Goo1.AuxVarErase "*3DGrenade_Cache"
	Goo1.AuxVarSetFromArr "*3DGrenade_Cache" aProjectiles

	; Process any new projectile refs and assign grenade icon
	if eval (Ar_Size aProjectiles) > 0 && aProjectiles != aCache
		aGrenades = Goo1.AuxVarGetAsArr "*3DGrenade_Ref"
		foreach aForE <- aProjectiles
			rProjectile = *aForE

			; If new grenade ref found
			if eval IsFormValid rProjectile && IsReference rProjectile
				rWeapon = rProjectile.GetProjectileRefWeapon
				if eval GetWeaponType rWeapon == 10 && rWeapon != TrapBaseball && (Ar_Find rProjectile aGrenades) == Ar_BadNumericIndex

					; Add icon tile
					if iTiles < 1
						AddTileFromTemplate "HUDMainMenu\3DGrenade|3DGRect|3DGRect"
						SetJohnnyOnRenderUpdateEventHandler 1 (CompileScript "3DGrenadeIndicator/OnRender.gek")
					endif
					Goo1.AuxVarSetRef "*3DGrenade_Ref" rProjectile iTiles
					AddTileFromTemplate "HUDMainMenu\3DGrenade\3DGRect|3DGIcon|3DGInj%g" (iTiles)

					; Set icon traits
					sUIPath = "HUDMainMenu\3DGrenade\3DGRect\3DGInj" + $iTiles
					if eval rProjectile.GetDetonationTimer < 0
						SetUIFloat (sUIPath + "\_ShowTimer") 0
					endif
					rExplosion = GetProjectileExplosion (rProjectile.GBO)
					SetUIFloatAlt (sUIPath + "\_RadiusDMG") (GetExplosionTraitNumeric rExplosion 2)

					; Set icon texture
					if eval GetExplosionTraitNumeric rExplosion 4 > 0
						SetUIStringAlt (sUIPath + "\Icon\filename") "Interface\Stentorious\3DGrenade\Rad.dds"
					elseif iB42Icon == 1
						sTexture = "textures\interface\xlrQT\" + $RefToString rWeapon + ".dds"
						Sv_Replace ":|" sTexture
						if FileExists (sTexture)
							Sv_Replace "textures\|" sTexture
							SetUIStringAlt (sUIPath + "\Icon\filename") (sTexture)
							if iB42Color == 1
								SetUIFloatAlt (sUIPath + "\_IconColor") 0
							endif
						endif
					endif

					iTiles += 1

				endif
			endif
		loop
	endif

	Goo1.AuxVarSetFlt "*3DGrenade_Flt" iTiles

	sv_Destruct sUIPath sTexture
	aGrenades = aProjectiles = aCache = aForE = Ar_Null

end