float fPosX
float fPosY
float fPosZ
ref rGrenade
array_var aGrenades
array_var aForE
string_var sUIPath

begin Function {}

	; Check for any tracked refs
	aGrenades = Goo1.AuxVarGetAsArr "*3DGrenade_Ref"
	if eval aGrenades != Ar_Null

		; Process refs and update any valid grenade icons
		foreach aForE <- aGrenades
			rGrenade = *aForE
			if eval IsFormValid rGrenade && IsReference rGrenade && GetWeaponType (rGrenade.GetProjectileRefWeapon) == 10

				; Update icon
				sUIPath = "HUDMainMenu\3DGrenade\3DGRect\3DGInj" + $aForE["key"]
				WorldToScreen (fPosX) (fPosY) (fPosZ) (0) (0) (0) (1) (rGrenade)
				SetUIFloatAlt (sUIPath + "\_x") fPosX
				SetUIFloatAlt (sUIPath + "\_y") fPosY
				SetUIFloatAlt (sUIPath + "\_Radius") (Player.GetDistance3D rGrenade)
				SetUIStringAlt (sUIPath + "\_Timer") "%.2f" (GetMaxOf 0 (rGrenade.GetDetonationTimer))

			endif
		loop

		sv_Destruct sUIPath
		aGrenades = aForE = Ar_Null

	endif
end