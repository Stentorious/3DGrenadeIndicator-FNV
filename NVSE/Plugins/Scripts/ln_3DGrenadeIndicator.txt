; 3D Grenade Indicator - Stentorious

; On load
; Reset all icons
SetJohnnyOnRenderUpdateEventHandler 0 (CompileScript "3DGrenadeIndicator\OnRender.gek")
UnloadUIComponent "HUDMainMenu\3DGrenade\Icons"
Goo1.AuxVarErase "*3DGrenade_Ref"
Goo1.AuxVarErase "*3DGrenade_Cache"
; Update perception calc
if eval GetUIFloatAlt "HUDMainMenu\3DGrenade\_DistMode"
	SetUIFloatAlt "HUDMainMenu\3DGrenade\_PlrAV6" (Player.GetThresholdAV Perception)
endif

; On game restart
if eval Goo1.AuxVarGetFlt "*3DGrenade_INI"
	return
endif
Goo1.AuxVarSetFlt "*3DGrenade_INI" 1

; Check for requirements
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 390
	MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 165
	MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "3D Grenade Indicator missing requirement!%rInstall latest User Interface Organizer plugin."
	return
endif

; Load INI settings
SetUIFloatAlt "HUDMainMenu\3DGrenade\_Icon" (GetINIFloat_Cached "General:iIconStyle" "Stentorious\3DGrenadeIndicator.ini" 1)
SetUIFloatAlt "HUDMainMenu\3DGrenade\_IconScale" (Clamp (GetINIFloat_Cached "General:fIconSize" "Stentorious\3DGrenadeIndicator.ini" 1) 0.5 2)
SetUIFloatAlt "HUDMainMenu\3DGrenade\_AlphaPulseToggle" (eval GetINIFloat_Cached "General:bPulsingIcon" "Stentorious\3DGrenadeIndicator.ini" 1)
SetUIFloatAlt "HUDMainMenu\3DGrenade\_Timer" (Clamp (GetINIFloat_Cached "General:iFuseTimer" "Stentorious\3DGrenadeIndicator.ini" 2) 0 3)
SetUIFloatAlt "HUDMainMenu\3DGrenade\_DistMax" (Clamp (GetINIFloat_Cached "General:fMaxDistance" "Stentorious\3DGrenadeIndicator.ini" 25) 5 100)
SetUIFloatAlt "HUDMainMenu\3DGrenade\_DistMode" (eval GetINIFloat_Cached "General:bPerceptionCalc" "Stentorious\3DGrenadeIndicator.ini" 0)

; B42 Quickthrow support
if eval FileExists "textures\interface\xlrQT\FalloutNV.esm4330.dds"
	Goo1.AuxVarSetFlt "*_3DGrenade_INI" (eval GetINIFloat_Cached "B42 Quickthrow:bUseIcons" "Stentorious\3DGrenadeIndicator.ini" 0) 0
	Goo1.AuxVarSetFlt "*_3DGrenade_INI" (eval GetINIFloat_Cached "B42 Quickthrow:bInterfaceTint" "Stentorious\3DGrenadeIndicator.ini" 0) 1
	Goo1.AuxVarSetFlt "*_3DGrenade_INI" 1 2
endif

; Update player perception
SetUIFloatAlt "HUDMainMenu\3DGrenade\_PlrAV6" (Player.GetThresholdAV Perception)
SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fOldValue, float fNewValue} => SetUIFloatAlt "HUDMainMenu\3DGrenade\_PlrAV6" (Player.GetThresholdAV Perception)) 0 6

; Alpha pulse
SetUIFloatGradual "HUDMainMenu\3DGrenade\_AlphaPulse" 0 0.65 0.3 2

; Projectile refs to ignore
AssignKeyword TrapBaseball "3DGrenadeBlacklist"

SetGameMainLoopCallback (CompileScript "3DGrenadeIndicator\MainLoop.gek") 1 10 1

CloseFileSO "Stentorious\3DGrenadeIndicator.ini" 0
